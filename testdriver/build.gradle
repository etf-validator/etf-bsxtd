///////////////////////////////////////////////////////////////////////////////////////
//
// ETF-BsxTD
//
///////////////////////////////////////////////////////////////////////////////////////

group = 'de.interactive_instruments.etf.testdriver'
description = "ETF BaseX test driver"


configurations {
	api
	bsxPlugins
	integrationTest
}

dependencies {

	compileOnly group: 'de.interactive_instruments.etf', name: 'etf-core', version:'2.1.0'+project.snapshotSuffix
	compileOnly group: 'de.interactive_instruments.etf', name: 'etf-spi', version:'2.1.0'+project.snapshotSuffix
	api group: 'org.basex', name: 'basex', version: basexVersion
	bsxPlugins project(path: ":gmlgeox", configuration: "xarFile")
    bsxPlugins project(path: ":topox", configuration: "xarFile")
    bsxPlugins project(path: ":testdriver-bridge", configuration: "xarFile")

	compile group: 'commons-logging', name: 'commons-logging', version:'1.1.1'
	compile group: 'commons-io', name: 'commons-io', version: etf_commonsIoVersion
	compileOnly group: 'net.sf.saxon', name: 'Saxon-HE', version: etf_saxonHe
	compileOnly group: 'org.slf4j', name: 'slf4j-api', version: etf_slf4jApiVersion

	if(Integer.valueOf(org.gradle.internal.jvm.Jvm.current().getJavaVersion().getMajorVersion())>8) {
		compile('javax.xml.bind:jaxb-api:2.3.0')
		compile('javax.activation:activation:1.1')
		compile('org.glassfish.jaxb:jaxb-runtime:2.3.0')
	}

	// Degree core for geometry validation
	compile("org.deegree:deegree-core-base:$deegreeVersion") {
		exclude group: 'org.slf4j'
		exclude group: 'xerces'
		exclude group: 'xml-apis'
		exclude group: 'com.h2database'
		exclude group: 'org.postgis'
		exclude group: 'postgresql'
		exclude group: 'org.postgresql'
		exclude group: 'commons-logging'
		exclude group: 'com.google.code.gson'
	}

	compile("org.geotools:gt-shapefile:$geoToolsVersion") {
		exclude group: 'javax.media'
	}
	compile("org.geotools.xsd:gt-xsd-wfs:$geoToolsVersion") {
		exclude group: 'javax.media'
		exclude group: 'org.apache.xml'
	}

	// Required by BaseX extensions, must be placed in the lib folder or
	// the extension class loader will not initialize 3rd party libraries correctly
	compileOnly group: 'xerces', name: 'xercesImpl', version: etf_xercesVersion
	compileOnly group: 'xml-apis', name: 'xml-apis', version: etf_xmlApisVersion

	integrationTest group: 'ch.qos.logback', name: 'logback-classic', version: etf_logbackVersion
	integrationTest group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: etf_junitTestVersion
	integrationTest group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: etf_junitTestVersion

	integrationTest group: 'de.interactive_instruments.etf', name: 'etf-bsxds', version: '2.1.0' + project.snapshotSuffix
	integrationTest group: 'de.interactive_instruments.etf', name: 'etf-core', version: '2.0.0' + project.snapshotSuffix
	integrationTest group: 'de.interactive_instruments.etf', name: 'etf-spi', version: '2.0.0' + project.snapshotSuffix
	integrationTest group: 'de.interactive_instruments.etf', name: 'etf-stdtot', version: '1.1.0' + project.snapshotSuffix
	integrationTest group: 'de.interactive_instruments.etf.test', name: 'etf-unittest', version: '1.1.1' + project.snapshotSuffix
	integrationTest group: 'de.interactive_instruments', name: 'ii-commons-util', version: '3.2.0' + project.snapshotSuffix
}

configurations {
	compile.exclude group:'xalan'
	runtime.exclude group: "org.slf4j", module: "slf4j-nop"

	compile.extendsFrom api
	testCompile.extendsFrom integrationTest
}

def allLibs = configurations.runtime + configurations.compile -
		configurations.compile.filter { it.name.startsWith('etf-core') } -
		configurations.compile.filter { it.name.startsWith('etf-spi') } -
		configurations.compile.filter { it.name.startsWith('ii-commons-util') } -
		configurations.compile.filter { it.name.startsWith('slf4j-api') } -
		configurations.compile.filter { it.name.startsWith('log4j') && !it.name.startsWith('log4j-over-slf4j') } -
		configurations.compile.filter { it.name.startsWith('jetty') } -
		configurations.compile.filter { it.name.startsWith('xml-apis') } -
		configurations.compile.filter { it.name.startsWith('commons-lang3') } -
		configurations.compile.filter { it.name.startsWith('xercesImpl') }

jar.doFirst {
	manifest {
		attributes(
				'Test-Engine-Title': 'BaseX',
				'Test-Engine-Vendor': 'BaseX GmbH',
				'Test-Engine-Version': basexVersion
		)
	}
}

jar {
	archivesBaseName = 'etf-bsxtd'

	into('lib') {
		from allLibs
	}

	into('plugins') {
		from configurations.bsxPlugins.copy().setTransitive(false)
		rename { String fileName ->
			fileName.replaceAll('-(\\d+)\\.(\\d+)\\.(\\d+)(-SNAPSHOT)?', "")
		}
	}
}

task deployTestDriver(dependsOn: jar) {
	doLast {
		copy {
			from jar
			into rootProject.testDeploymentDir
		}
		println "Copied test driver to $rootProject.testDeploymentDir"
	}
}

install {
	dependsOn = ["deployTestDriver"]
}

// INTEGRATION TESTS
///////////////////////////////////////////////////////////////////////////////////////

// Drop classloader for integration tests
sourceSets {
	test {
		java {
			compileClasspath += main.output
			runtimeClasspath = configurations.api + test.output + configurations.integrationTest
		}
	}
}

test {
	dependsOn = ["testClasses", "compileTestJava", "deployTestDriver"]
}

///////////////////////////////////////////////////////////////////////////////////////
